<!DOCTYPE html>
<html>
<head>
<meta http-equiv="x-ua-compatible" content="ie=10">
<meta charset="gbk">
<meta http-equiv="content-type" content="text/html;charset=gbk">
<title>光盘批量自动复制工具</title>
<style type="text/css">
body {cursor:default;line-height:150%;}
</style>
<HTA:APPLICATION
	ID="CDROM_Auto_XCopy_Tool"
	APPLICATIONNAME="CDROM_Auto_XCopy_Tool"
	BORDER = "dialog"
	BORDERSTYLE = "normal"
	CAPTION="normal"
	ContextMenu="no"
	ICON="%SystemRoot%/system32/cleanmgr.exe"
	MaximizeButton="no"
	MinimizeButton="no"
	SCROLL="no"
	SINGLEINSTANCE="yes"
	WINDOWSTATE="normal"
	innerBorder="no">
</head>
<SCRIPT Language="VBScript">
' resize to quarter of screen area, centered
window.resizeTo 500,600
window.moveTo screen.availWidth/4,screen.availHeight/4

Const ForReading = 1, ForWriting = 2, ForAppending = 8
Set objFSO = CreateObject("Scripting.FileSystemObject")
Set objRegEx = CreateObject("VBScript.RegExp")
objRegEx.Global = True
Set oShell = CreateObject ("WScript.Shell")
Set objApp = CreateObject("shell.application")
Set sapi = CreateObject("sapi.spvoice")

Set wmpObject = CreateObject("WMPlayer.OCX.7")
Set cdroms = wmpObject.cdromCollection
CDROM = "MY COMPUTER"
CDROM_Count = 0

Const bOut = True
Const bIn = False
CDROM_State = bIn

targetFolder = "MY COMPUTER"

iWaitTime = 0
iInterval_ID = 0

Sub EjectCD(bState)
	If CDROM_State <> bState And cdroms.Count >= 1 Then
		For z = 0 to cdroms.Count - 1
			cdroms.Item(z).eject
		Next
		CDROM_State = bState
	End If
	If CDROM_State = bOut Then
		txtInfo = "请放入光盘，等待手动弹入或者自动收回"
		appInfo.innertext = txtInfo
		sapi.Speak txtInfo, 1
	Else
		txtInfo = "光盘已经收回，正在读取，请勿作任何操作！"
		appInfo.innertext = txtInfo
		sapi.Speak txtInfo, 1
	End If
End Sub

Sub WaitCD()
	iWaitTime = iWaitTime + 3
	if iWaitTime > twaittime.value Then
		EjectCD bIn
		If objFSO.GetDrive(CDROM).IsReady then
			'window.clearTimeOut iInterval_ID
			'If Not objFSO.GetDrive(CDROM).IsReady Then EjectCD bIn
			txtInfo = "光盘" & objFSO.GetDrive(CDROM).VolumeName & "已经准备好，开始读取数据。"
			sapi.Speak txtInfo, 1
			appInfo.innertext = txtInfo
			CopyCD
		Else
			If AskContinue = 7 Then 'vbNo
				txtInfo = "复制光盘结束"
				sapi.Speak txtInfo, 1
				appInfo.innertext = txtInfo
				window.clearTimeOut iInterval_ID
				Exit Sub
			End If
		End If
		iWaitTime = 0
		EjectCD bOut
	End If
	iInterval_ID = window.setTimeOut("WaitCD()",3000)
End Sub

Sub SelectCDROM()
	'选择光驱
	CDROM = BrowseFolder(CDROM,"选择光驱",True)
	cdrompath.value = CDROM
	If instr(cdrompath.value,"\")>0 And instr(folderpath.value,"\")>0 Then
		Run_Button.disabled = False
	End If
End Sub

Sub SelectFolder()
	'选择目标文件夹
	targetFolder = BrowseFolder(targetFolder,"选择目标文件夹",True)
	folderpath.value = targetFolder
	If instr(cdrompath.value,"\")>0 And instr(folderpath.value,"\")>0 Then
		Run_Button.disabled = False
	End If
End Sub

Sub CDROM_XCopy()
	'EjectCD bOut
	WaitCD
End Sub

Function AskContinue()
	sapi.Speak "没有发现光盘，还要继续处理吗？",1
	'1 = SVSFlagsAsync 异步处理。
	Answer = MsgBox("没有发现光盘，还要继续处理么？",68,"复制光盘")
	'If Answer = 7 Then 'vbNo
	AskContinue = Answer
End Function

Sub CopyCD()
	CDROM_Count = CDROM_Count +1
	CDROM_number = right("00" & CDROM_Count,2)
	objRegEx.Pattern = "[\\\/\:\*\?\" & """" & "\<\>\|]"
	CDROM_Name = objFSO.GetDrive(CDROM).VolumeName
	CDROM_Name = objRegEx.Replace(CDROM_Name," ")

	If Rforce(1).checked OR Trim(CDROM_Name) = "" Then
		CDROM_Name = cdromname.value & "-" & CDROM_number 
			'& "-" & Replace(Date(),"-","")& Replace(Time(),":","")
	Else
		'如果卷标不为空，可以考虑判断是否重复
		CDROM_Name = CDROM_Name & "-" & CDROM_number
	End If

	New_Folder = targetFolder & "\" & CDROM_Name
	If Not objFSO.FolderExists(New_Folder) Then
		objFSO.CreateFolder New_Folder
	End If
	
	set objTargetFolder = objApp.NameSpace(New_Folder & "\")
	
	if not objTargetFolder is nothing then
		objTargetFolder.CopyHere CDROM & "*.*",16+512
		'16 = Click "Yes to All" in any dialog box displayed.
		'512 = Do not confirm the creation of a new directory
	end if

	sapi.Speak "复制光盘完成",1
End Sub
Function BrowseFolder( myStartLocation, strPrompt, blnSimpleDialog )
'打开“浏览文件夹对话框”
	Const MY_COMPUTER   = &H11&
	Const WINDOW_HANDLE = 0 '此处必须为0
	
	Dim numOptions, objFolder, objFolderItem
	Dim objPath, objShell, strPath
	
	'为对话框设置参数
	If strPrompt = "" Then
		strPrompt = "Select a folder:"
	End If
	
	If blnSimpleDialog = True Then
		numOptions = 0      '简单对话框
	Else
		numOptions = &H10&  '添加一个输入文件夹路径的输入框
	End If
	
	Set objShell = CreateObject( "Shell.Application" )
	
	If UCase( myStartLocation ) = "MY COMPUTER" Then
		Set objFolder = objShell.Namespace( MY_COMPUTER )
		Set objFolderItem = objFolder.Self
		strPath = objFolderItem.Path
	Else
		strPath = myStartLocation
	End If
	
	Set objFolder = objShell.BrowseForFolder( WINDOW_HANDLE, strPrompt, numOptions, strPath )
	If objFolder Is Nothing Then
		BrowseFolder = ""
		Exit Function
	End If
	
	Set objFolderItem = objFolder.Self
	objPath = objFolderItem.Path
	BrowseFolder = objPath
End Function

Sub Window_Onload()
	
End Sub

Sub nwaittime_onkeypress()
	If (window.event.keyCode < 45 OR window.event.keyCode > 57) Then
		window.event.returnValue = false
	End If
End Sub

Sub twaittime_onchange()
	document.all("lwait").innertext = document.all("twaittime").value
	'msgbox document.all("twaittime").value
End Sub

Sub ExitHTA
    self.close()
End Sub
</SCRIPT>
<body STYLE="background-color:buttonface;">
<div name="appBody" id="appBody" style="display:block;">
此软件执行以下几件事情：
<hr />
选定来源光驱：
<br>
<input type="text" name="cdrompath" id="cdrompath" size="30" value="" disabled>
<button accesskey="C" onclick="SelectCDROM()">来源光驱(<u>C</u>)</button>
<br>
选定本地目标文件夹：
<br>
<input type="text" name="folderpath" id="folderpath" size="30" value="" disabled>
<button accesskey="F" onclick="SelectFolder()">目标文件夹(<u>F</u>)</button>
<hr>
光驱托盘弹出后等待时间：<br>
<!-- 
<input type="text" name="nwaittime" id="nwaittime" style="ime-mode:disabled" value="10" size="2">秒钟。<br>
 -->
<input type="range" name="twaittime" id="twaittime" min="5" max="30" step="5" value="10"> <label name="lwait" id="lwait" for="twaittime" accesskey="M">10</label>秒钟(<u>M</u>)。
<hr>
默认新建文件夹名是光盘卷标，<br>
<input type="radio" name="Rforce" id="notforce" value="notforce" checked><label for="notforce">如果光盘没有卷标，则使用自定义文件夹名前缀。<br>
<input type="radio" name="Rforce" id="forcefolder" value="force"><label for="forcefolder">无论如何都强制使用自定义文件夹名前缀。<br>
<label for="cdromname">自定义文件夹名前缀：</label><input type="text" name="cdromname" id="cdromname" value="cdrom" size="10">，后自动接光盘顺序编号。
</div>
<hr />
<Div name="infIn" id="infIn" style="display:block;">光驱托盘已经收起。</Div>
<Div name="infCopy" id="infCopy" style="display:none;">光驱托盘已经收起，正在读取。请不要强行手动弹出托盘。</Div>
<Div name="infOut" id="infOut" style="display:none;">光驱托盘已经弹出，请不要手动收起光驱托盘。</Div>
<div>如果光驱内没有放入光盘，则自动结束。</div>
<hr />
<div align="center">
<button name="Run_Button" onClick="CDROM_XCopy" accesskey="S" disabled>开始(<u>S</u>)</button>&nbsp;&nbsp;
<button onClick="self.close()" accesskey="X">退出(<u>X</u>)</button>
</div>
</body>
</html>